/* A simplified version of the SumProduct Puzzle taken from
http://www.nytimes.com/2015/04/15/science/a-math-problem-from-singapore-goes-viral-when-is-cheryls-birthday.html?_r=0
*/
 
 /* Albert and Bernard just met Cheryl. "When’s your birthday?" Albert asked Cheryl.*/
 Month:STRING & Day:NATURAL1 & 
/* Cheryl thought a second and said, :I’m not going to tell you, but I’ll give you some clues. She wrote down a list of 10 dates: */
PD = {("aug"|->14),("aug"|->15),("aug"|->17),("july"|->14),("july"|->16),("june"|->17),("june"|->18),("may"|->15),("may"|->16),("may"|->19)}
&
/*
Then Cheryl whispered in Albert’s ear the month — and only the month — of her birthday. To Bernard, she whispered the day, and only the day. 
*/
 Month : dom(PD) &
 Day : ran(PD) &
 Month|->Day : PD &

/* Albert: I don’t know when your birthday is, */
card(PD[{Month}]) > 1 &
/* but I know Bernard doesn’t know, either. */
!x.(x:PD[{Month}] => card(PD~[{x}]) > 1) &

/* Bernard: I didn’t know originally, */
 card(PD~[{Day}]) > 1  &
/* but now I do. */
 PD2 = {m,d| (m|->d):PD &  !x.(x:PD[{m}] => card(PD~[{x}]) > 1 ) } &
 card(PD2~[{Day}]) = 1&
 
 /* Albert: Well, now I know, too! */
  card({d|Month|->d : PD2 & card(PD2~[{d}]) = 1})=1